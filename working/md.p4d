//
// peg for markdown
// ("GFM" means "GitHub Flavored Markdown".)
//

// Todo: Text複数行対応

File
	= Statements !.

Chunk
	= Quote

TopLevel
	= _ Statements _

Statements
	= { (@Quote)+ #source }

_
	= (!Nl [ \t])*

BREAK
	= '\n' / '\r\n' / '\r'

DIGIT
	= [0-9]

Quote
	= {'>' #quote} @Quote
	/ Statement

Statement
	= Hr / Code / Item / Head / ( Nl BREAK ) / Text / Blank

Code
	= { ( [ \t] [ \t] [ \t] [ \t]+ '`' @{ (!'`' .)* #code } '`' _ eos)+ #multilinecode }
	/ '`' { @{ (!'`' !BREAK .)* #code } #singlelinecode } '`' _ eos

Item
	= { [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t]+ ('*'/'+'/'-') [ \t]+ @NoBreakText _ eos #ul_li_3 }
	/ { [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t]+ DIGIT+ '.' [ \t]+ @NoBreakText _ eos #ol_li_3 }
	/ { [ \t] [ \t] [ \t] [ \t]+ ('*'/'+'/'-') [ \t]+ @NoBreakText _ eos #ul_li_2 }
	/ { [ \t] [ \t] [ \t] [ \t]+ DIGIT+ '.' [ \t]+ @NoBreakText _ eos #ol_li_2 }
	/ { _ ('*'/'+'/'-') [ \t]+ @NoBreakText _ eos #ul_li_1 }
	/ { _ DIGIT+ '.' [ \t]+ @NoBreakText _ eos #ol_li_1 }

Head
	= { '######' _ @NoBreakText #h6 } _ '#'* _ eos
	/ { '#####' _ @NoBreakText #h5 } _ '#'* _ eos
	/ { '####' _ @NoBreakText #h4 } _ '#'* _ eos
	/ { '###' _ @NoBreakText #h3 } _ '#'* _ eos
	/ { '##' _ @NoBreakText #h2 } _ '#'* _ eos
	/ { '#' _ @NoBreakText #h1 } _ '#'* _ eos
	/ { @NoBreakText _ eos _ '-'+ _ eos #h2 }
	/ { @NoBreakText _ eos _ '='+ _ eos #h1 }

Nl
	= [ \t] [ \t] &BREAK { #br }

eos
	= (@Nl)? _ BREAK

Hr
	= _ '*' _ '*' _ '*' ( _ '*')* _ eos { #hr }
	/ _ '-' _ '-' _ '-' ( _ '-')* _ eos { #hr }
	/ _ '_' _ '_' _ '_'( _ '_')* _ eos { #hr }

Text
	= { (@TextMaterial)+ BREAK? #text }

NoBreakText
	= { (@TextMaterial)+ #text }

TextMaterial
	= { '[' @{ (!']' .)* #name } ']:' #a }
	/ { '[' @{ (@NoBLetter)* #alt } '][' @{ (!']' .)* #href } ']' #a }
	/ { '[' @{ (@NoBLetter)* #alt } '](' @{ (!')' .)* #href } ')' #a }
	/ { '![' @{ (@NoBLetter)* #alt } '](' @{ (!')' .)* #href } ')' #img }
//	/ { @{ 'http' [-_#\.!'()%A-Za-z0-9] #href } #a }		//GFM
	/ Letter

Letter
	= '~~' { (!'~~' TEXT)* #strike } '~~'		//GFM
	/ '**' { (!'**' TEXT)* #bold } '**'
	/ '*' { (!'*' TEXT)* #italic } '*'
	/ '__' { (!'__' TEXT)* #bold } '__'
	/ '_' { (!'_' TEXT)* #italic } '_'
	/ { TEXT+ #span }

NoBLetter
	= '~~' { (!'~~' !'](' TEXT)* #strike } '~~'		//GFM
	/ '**' { (!'**' !'](' TEXT)* #bold } '**'
	/ '*' { (!'*' !'](' TEXT)* #italic } '*'
	/ '__' { (!'__' !'](' TEXT)* #bold } '__'
	/ '_' { (!'_' !'](' TEXT)* #italic } '_'
	/ { (!'](' TEXT)+ #span }

TEXT
	= !'![' !'[' !']' !BREAK !([ \t] [ \t] BREAK) .

Blank
	= _ BREAK { #emptyLine }

//	[\x21-\x29\x2C\x2D\x2F-\x5A\x5E\x5F\x61-\x7E\\\.]