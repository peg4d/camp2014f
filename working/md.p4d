//
// peg for markdown
// ("GFM" means "GitHub Flavored Markdown".)
//

// Todo: Text複数行対応

File
	= Statements !.

Chunk
	= Quote

TopLevel
	= _ Statements _

Statements
	= { (@Quote)+ #source }

_
	= (!NL [ \t])*

BREAK
	= '\n' / '\r\n' / '\r'

NL
	= [ \t] [ \t] &BREAK

DIGIT
	= [0-9]

Quote
	= {'>' #quote} @Quote
	/ Statement

Statement
	= Hr / Code / Item / Head / Text / Blank

Code
	= { ( [ \t] [ \t] [ \t] [ \t]+ '`' @{ (!'`' .)* #code } '`' _ eos )+ #multilinecode }
	/ '`' { @{ (!'`' !BREAK .)* #code } #singlelinecode } '`' _ eos

Item
	= [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t]+ ('*'/'+'/'-') [ \t]+ { (!BREAK !NL .)* #ul_li_3 } _ eos
	/ [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t] [ \t]+ DIGIT+ '.' [ \t]+ { (!BREAK !NL .)* #ol_li_3 } _ eos
	/ [ \t] [ \t] [ \t] [ \t]+ ('*'/'+'/'-') [ \t]+ { (!BREAK !NL .)* #ul_li_2 } _ eos
	/ [ \t] [ \t] [ \t] [ \t]+ DIGIT+ '.' [ \t]+ { (!BREAK !NL .)* #ol_li_2 } _ eos
	/  _ ('*'/'+'/'-') [ \t]+ { (!BREAK !NL .)*#ul_li_1 } _ eos
	/ _ DIGIT+ '.' [ \t]+ { (!BREAK !NL .)* #ol_li_1 } _ eos

Head
	= { '######' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h6 }
	/ { '#####' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h5 }
	/ { '####' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h4 }
	/ { '###' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h3 }
	/ { '##' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h2 }
	/ { '#' _ @{ (!BREAK !NL !'#' .)* #span } _ '#'* _ eos #h1 }
	/ { @{ (!BREAK !NL .)* #span } _ eos _ '-'+ _ eos #h2 }
	/ { @{ (!BREAK !NL .)* #span } _ eos _ '='+ _ eos #h1 }

Hr
	= _ '*' _ '*' _ '*' ( _ '*')* _ eos { #hr }
	/ _ '-' _ '-' _ '-' ( _ '-')* _ eos { #hr }
	/ _ '_' _ '_' _ '_'( _ '_')* _ eos { #hr }

Blank
	= _ BREAK { #emptyLine }

NewLine
	= NL { #br }

eos
	= (@NewLine)? BREAK

Text
	= { (@TextMaterial)+ _ eos #text }

NoBreakText
	= { (@TextMaterial)+ #text }

TextMaterial
	= { '[' @{ (!']' .)* #name } ']:' #a }
	/ { '[' @{ (@NoBLetter)* #alt } '][' @{ (!']' .)* #href } ']' #a }
	/ { '[' @{ (@NoBLetter)* #alt } '](' @{ (!')' .)* #href } ')' #a }
	/ { '![' @{ (@NoBLetter)* #alt } '](' @{ (!')' .)* #href } ')' #img }
//	/ { @{ 'http' [-_#\.!'()%A-Za-z0-9] #href } #a }		//GFM
	/ Letter

Letter
	= '~~' { (!'~~' TEXT)* #strike } '~~'		//GFM
	/ '**' { (!'**' TEXT)* #bold } '**'
	/ '*' { (!'*' TEXT)* #italic } '*'
	/ '__' { (!'__' TEXT)* #bold } '__'
	/ '_' { (!'_' TEXT)* #italic } '_'
	/ { TEXT+ #span }

NoBLetter
	= '~~' { (!'~~' !'](' TEXT)* #strike } '~~'		//GFM
	/ '**' { (!'**' !'](' TEXT)* #bold } '**'
	/ '*' { (!'*' !'](' TEXT)* #italic } '*'
	/ '__' { (!'__' !'](' TEXT)* #bold } '__'
	/ '_' { (!'_' !'](' TEXT)* #italic } '_'
	/ { (!'](' TEXT)+ #span }

TEXT
	= !'![' !'[' !']' !NL !BREAK .

//	[\x21-\x29\x2C\x2D\x2F-\x5A\x5E\x5F\x61-\x7E\\\.]