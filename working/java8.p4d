
// PEG4d Java8 Grammar
// ===============

export
	= Statement
	/ Expression
	/ Constant

File
	= _ { (@PackageDeclaration)? ( _ @ImportDeclaration)* ( _ @TypeDeclaration)* #Source } _ EOT


Chunk
	= TopLevel
	/ Statement
	/ Expression
	/ Type

_
	= (S / BLOCKCOMMENT / LINECOMMENT )*

S
	= [ \n\r\t\u000B\u000C]

BLOCKCOMMENT
	= '/*' (!'*/' .)* '*/'

LINECOMMENT
	= '//' (!'\n' .)*


EOL
	= ('\r'? '\n' / !.)

DIGIT
	= [0-9]

W
	= [a-zA-Z0-9_$]

HEX
	= [a-fA-F0-9]

HEX4
	= HEX HEX HEX HEX

Name
	= { NAME #Name }

QualifiedName
	= Name {@ _ "." _ @Name #QualifiedName }*

NAME
	= !DIGIT !(KEYWORD !W) W+

KEYWORD
	= 'abstract' / 'assert' / 'boolean' / 'break' / 'byte' / 'case'
	/ 'catch' / 'char' / 'class' / 'const' / 'continue' / 'default'
	/ 'double' / 'do' / 'else' / 'enum' / 'extends' / 'false'
	/ 'final' / 'finally' / 'float' / 'for' / 'goto' / 'if' / 'implements'
	/ 'import' / 'instanceof' / 'interface' / 'int' / 'long' / 'native'
	/ 'new' / 'null' / 'package' / 'private' / 'protected' / 'public'
	/ 'return' / 'short' / 'static' / 'strictfp' / 'super' / 'switch'
	/ 'synchronized' / 'this' / 'throws' / 'throw' / 'transient'
	/ 'true' / 'try' / 'void' / 'volatile' / 'while'

"++"   = '++'
"--"   = '--'
"&"    = '&' ![&=]
"*"    = '*' !'='
"+"    = '+' ![+=]
"-"    = '-' !('-' / [=>])
"~"    = '~'
"!"    = '!' !'='

"{"    = '{'
"["    = '['
"]"    = ']'
","    = ','
"..."  = '...'
":"    = ':' ![>]
"/"    = '/' !'='
"%"    = '%' ![=>]
"<<"   = '<<' !'='
">>"   = '>>' ![=>]
">>>"  = '>>>' !'='
"<="   = '<='
">="   = '>='
"<"    = '<' ![=]
">"    = '>' ![=]
"=="   = '=='
"!="   = '!='
"^"    = '^' ![=]
"|"    = '|' ![|=]
"&&"   = '&&'
"||"   = '||'
"?"    = '?'
";"    = ';'
"}"    = '}'

"*="   = '*='
"/="   = '/='
"%="   = '%='
"+="   = '+='
"-="   = '-='
"<<="  = '<<='
">>="  = '>>='
">>>=" = '>>>='
"&="   = '&='
"^="   = '^='
"|="   = '|='
"="    = '=' !'='
"."    = '.' !'.'

"("    = '('
")"    = ')'

"@"    = '@'
"->"   = '->'

// reserved keyword
"abstract"     = 'abstract'     !W
"assert"       = 'assert'       !W
"boolean"      = 'boolean'      !W
"break"        = 'break'        !W
"byte"         = 'byte'         !W
"case"         = 'case'         !W
"catch"        = 'catch'        !W
"char"         = 'char'         !W
"class"        = 'class'        !W
"const"        = 'const'        !W
"continue"     = 'continue'     !W
"default"      = 'default'      !W
"do"           = 'do'           !W
"double"       = 'double'       !W
"else"         = 'else'         !W
"enum"         = 'enum'         !W
"extends"      = 'extends'      !W
"false"        = 'false'        !W
"final"        = 'final'        !W
"finally"      = 'finally'      !W
"float"        = 'float'        !W
"for"          = 'for'          !W
"goto"         = 'goto'         !W
"if"           = 'if'           !W
"implements"   = 'implements'   !W
"import"       = 'import'       !W
"instanceof"   = 'instanceof'   !W
"int"          = 'int'          !W
"interface"    = 'interface'    !W
"long"         = 'long'         !W
"native"       = 'native'       !W
"new"          = 'new'          !W
"null"         = 'null'         !W
"package"      = 'package'      !W
"private"      = 'private'      !W
"protected"    = 'protected'    !W
"public"       = 'public'       !W
"return"       = 'return'       !W
"short"        = 'short'        !W
"static"       = 'static'       !W
"strictfp"     = 'strictfp'     !W
"super"        = 'super'        !W
"switch"       = 'switch'       !W
"synchronized" = 'synchronized' !W
"this"         = 'this'         !W
"throw"        = 'throw'        !W
"throws"       = 'throws'       !W
"transient"    = 'transient'    !W
"true"         = 'true'         !W
"try"          = 'try'          !W
"void"         = 'void'         !W
"volatile"     = 'volatile'     !W
"while"        = 'while'        !W


EOT = !.

TopLevel
	= PackageDeclaration
	/ ImportDeclaration
	/ TypeDeclaration


// package definition
PackageDeclaration
	= { (@Annotations _ )? "package" _ @QualifiedName _ ";" #PackageDeclaration }

// import definition
ImportDeclaration
	= TypeImportOnDemandDeclaration
	/ SingleTypeImportDeclaration
	/ StaticImportOnDemandDeclaration
	/ SingleStaticImportDeclaration

SingleTypeImportDeclaration
	= "import" _ { @ClassOrInterfaceTypeName #SingleTypeImportDeclaration } _ ";"

TypeImportOnDemandDeclaration
	= "import" _ { @QualifiedName _ "." _ "*" #TypeImportOnDemandDeclaration } _ ";"

SingleStaticImportDeclaration
	= "import" _ "static" _ { @ClassOrInterfaceTypeName #SingleStaticImportDeclaration } _ ";"

StaticImportOnDemandDeclaration
	= "import" _ "static" _ { @ClassOrInterfaceTypeName #StaticImportOnDemandDeclaration } _ "." _ "*" _ ";"

// type definition
TypeDeclaration
	= ClassDeclaration
	/ InterfaceDeclaration
	/ { ";" #Empty }

// annotation definition
Annotation
	= NormalAnnotation
	/ SingleElementAnnotation
	/ MarkerAnnotation

Annotations
	= { @Annotation ( _ @Annotation)* #List }

NormalAnnotation
	= "@" _ { @ClassOrInterfaceTypeName _ "(" _ (@ElementValuePairList)? _ ")" #NormalAnnotation }

MarkerAnnotation
	= "@" _ { @ClassOrInterfaceTypeName #MarkerAnnotation }

SingleElementAnnotation
	= "@" _ { @ClassOrInterfaceTypeName _ "(" _ @ElementValue _ ")" #SingleElementAnnotation }

ElementValuePairList
	= { @ElementValuePair ( _ "," _ @ElementValuePair)* #ElementValuePairList }

ElementValuePair
	= { @Name _ "=" _ @ElementValue #ElementValuePair }

ElementValue
	= ElementValueArrayInitializer
	/ ConditionalExpression
	/ Annotation

ElementValueArrayInitializer
	= "{" _ { (@ElementValueList)? #ElementValueArrayInitializer } _ ","? _ "}"

ElementValueList
	= { @ElementValue ( _ "," _ @ElementValue)* #ElementValueList}


// Declaration
//==============

//local variable
VariableDeclaration
	= { (@[0]VariableModifiers _ )? @[1]Type _ @[2]{ @InitDecl ( _ "," _ @InitDecl)* #List } #VarDeclStmt }

VariableModifier
	= Annotation
	/ { "final" #Text }

VariableModifiers
	= { @VariableModifier ( _ @VariableModifier)* #List }

InitDecl
	= { @[0]VarName ( _ "=" _ @[1]Initializer)? #VarDecl }

InitDeclAssign
	= { @[0]VarName _ "=" _ @[1]Initializer #VarDecl }

VarName
	= Name {@ _ (@[1]Annotations)? _ "[" _ "]" #ArrayName }*

Initializer
	= Expression
	/ ArrayInitializer

InitializerList
	= { @Initializer ( _ "," _ @Initializer)* #List }

ArrayInitializer
	= { "{" _ (@InitializerList)? _ ","? _ "}" #Array }

//class type, enum type
ClassDeclaration
	= { (@[0]ClassModifiers _ )? "class" _ @[1]Name _ (@[2]TypeParameters)? _ (@[3]SuperClass)? _ (@[4]SuperInterfaces _ )* @[5]ClassBody #Class }
	/ { (@[0]ClassModifiers _ )? "enum" _ @[1]Name _ (@[4]SuperInterfaces _ )* @[5]EnumBody #Enum }

ClassModifier
	= Annotation
	/ { "public"    #Text }
	/ { "protected" #Text }
	/ { "private"   #Text }
	/ { "abstract"  #Text }
	/ { "final"     #Text }
	/ { "static"    #Text }
	/ { "strictfp"  #Text }

ClassModifiers
	= { @ClassModifier ( _ @ClassModifier)* #List }

SuperClass
	= "extends" _ { @ClassOrInterfaceType #SuperClass }

SuperInterfaces
	= "implements" _ { @ClassOrInterfaceTypes #SuperInterfaces }

EnumBody
	= "{" _ { (@EnumeratorList)? _ ","? _ ";" ( _ @ClassBodyDeclaration)+ #EnumBody } _ "}"
	/ "{" _ { (@EnumeratorList)? _ ","? _ ";"? #EnumBody } _ "}"

EnumeratorList
	= { @Enumerator ( _ "," _ @Enumerator)* #List }

Enumerator
	= { (@Annotations _ )? @Name ( _ "(" _ @ArgumentList _ ")")? ( _ @ClassBody)? #Enumerator }

ClassBody
	= "{" _ { (@ClassBodyDeclaration _ )* #Block } "}"

ClassBodyDeclaration
	= ClassMemberDeclaration
	/ { @Block #InstanceInisializer }
	/ "static" _ { @Block #StaticInitializer }
	/ ConstructorDeclaration

ClassMemberDeclaration
	= FieldDeclaration
	/ MethodDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration
	/ { ";" #Empty }

//interface, annotation type
InterfaceDeclaration
	= { (@InterfaceModifiers _ )? "interface" _ @Name _ (@TypeParameters)? _ (@ExtendsInterfaces _ )* @InterfaceBody #InterfaceDeclaration }
	/ { (@InterfaceModifiers _ )? "@" _ "interface" _ @Name _ @AnnotationTypeBody #AnnotationTypeDeclaration }

InterfaceModifier
	= Annotation
	/ { "public"    #Text }
	/ { "protected" #Text }
	/ { "private"   #Text }
	/ { "abstract"  #Text }
	/ { "static"    #Text }
	/ { "strictfp"  #Text }

InterfaceModifiers
	= { @InterfaceModifier ( _ @InterfaceModifier)* #List }

ExtendsInterfaces
	= { "extends" _ @ClassOrInterfaceTypes #ExtendsInterfaces }

InterfaceBody
	= "{" _ { (@InterfaceMemberDeclaration _ )* #InterfaceBody } "}"

InterfaceMemberDeclaration
	= ConstantDeclaration
	/ InterfaceMethodDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration
	/ { ";" #Empty }

AnnotationTypeBody
	= "{" _ { (@AnnotationTypeMemberDeclaration _ )* #AnnotationTypeBody } "}"

AnnotationTypeMemberDeclaration
	= AnnotationTypeElementDeclaration
	/ ConstantDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration
	/ { ";" #Empty }

AnnotationTypeElementDeclaration
	= { (@AnnotationTypeElementModifiers _ )? @Type _ @Name _ "(" _ ")" _ (@AnnotationTypeElementDefaultValue)? _ ";" #AnnotationTypeElement }

AnnotationTypeElementModifier
	= Annotation
	/ { "public"   #Text }
	/ { "abstract" #Text }

AnnotationTypeElementModifiers
	= { @AnnotationTypeElementModifier ( _ @AnnotationTypeElementModifier)* #List }

AnnotationTypeElementDefaultValue
	= "default" _ ElementValue


//field, constant
FieldDeclaration
	= { (@FieldModifiers _ )? @[1]Type _ @[2]{ @InitDecl ( _ "," _ @InitDecl)* #List } #VarDeclStmt } _ ";"

FieldModifier
	= ConstructorModifier
	/ { "final"     #Text }
	/ { "static"    #Text }
	/ { "transient" #Text }
	/ { "volatile"  #Text }

FieldModifiers
	= { @FieldModifier ( _ @FieldModifier)* #List }

ConstantDeclaration
	= { (@ConstantModifiers _ )? @Type _ @[2]{ @InitDecl ( _ "," _ @InitDecl)* #List } #Constant } _ ";"

ConstantModifier
	= Annotation
	/ { "public" #Text }
	/ { "final"  #Text }
	/ { "static" #Text }

ConstantModifiers
	= { @ConstantModifier ( _ @ConstantModifier)* #List }

//method
MethodDeclaration
	= { (@[0]MethodModifiers _ )? (@[3]TypeParameters _ )? @[1]TypeOrVoid _ @[2]Name _ "(" _ @[4]MethodParamList _ ")" _ (@[5]Throws)? _ (@[6]Block / ";" ) #FuncDecl }

MethodModifier
	= ConstructorModifier
	/ { "abstract"     #Text }
	/ { "final"        #Text }
	/ { "static"       #Text }
	/ { "synchronized" #Text }
	/ { "native"       #Text }
	/ { "strictfp"     #Text }


MethodModifiers
	= { @MethodModifier ( _ @MethodModifier)* #List }

InterfaceMethodDeclaration
	= { (@[0]InterfaceMethodModifiers _ )? (@[3]TypeParameters _ )? @[1]TypeOrVoid _ @[2]Name _ "(" _ @[4]MethodParamList _ ")" _ (@[5]Throws)? _ (@[6]Block / ";" ) #FuncDecl }

InterfaceMethodModifier
	= Annotation
	/ { "public"   #Text }
	/ { "abstract" #Text }
	/ { "default"  #Text }
	/ { "static"   #Text }
	/ { "strictfp" #Text }

InterfaceMethodModifiers
	= { @InterfaceMethodModifier ( _ @InterfaceMethodModifier)* #List }

MethodParamList
	= { (@ReceiverParam _ "," _ )? (@MethodParam _ "," _ )* @VarArgs #List }
	/ { @ReceiverParam ( _ "," _ @MethodParam)+ #List }
	/ { ( @MethodParam ( _ "," _ @MethodParam)* )? #List }

MethodParam
	= { (@[0]VariableModifiers _ )? @[1]Type _ @[2]VarName #Parameter }

ReceiverParam
	= { (@[0]Annotations _ )? @[1]Type _ @[2]{ (@Name _ "." )? _ @This #Field } #Parameter }

VarArgs
	= { (@VariableModifiers _ )? @Type _ "..." _ @VarName #VariadicParameter }

Throws
	= "throws" _ { @ClassOrInterfaceTypes #Throws }

//constructor
ConstructorDeclaration
	= { (@[0]ConstructorModifiers _ )? (@[3]TypeParameters _ )? @[2]Name _ "(" _ @[4]MethodParamList _ ")" _ (@[5]Throws)? _ @[6]ConstructorBody #FuncDecl }

ConstructorModifier
	= Annotation
	/ { "public"    #Text }
	/ { "protected" #Text }
	/ { "private"   #Text }

ConstructorModifiers
	= { @ConstructorModifier ( _ @ConstructorModifier)* #List }

ConstructorBody
	= { "{" _ (@ExplicitConstructorInvocation)? _ ((@Statement / @LocalDeclaration) _ )* "}" #Block }

ExplicitConstructorInvocation
	= { (@[2]TypeArguments _ )? @[0]This  _ "(" _ @[1]ArgumentList _ ")" #Apply } _ ";"
	/ { (@[2]TypeArguments _ )? @[0]Super _ "(" _ @[1]ArgumentList _ ")" #Apply } _ ";"
	/ { @[0]PostfixExpression _ "." _ (@[3]TypeArguments)? _ @[1]Super _ "(" _ @[2]ArgumentList _ ")" #Method } _ ";"


//Type
//====

Type
	= ReferenceType / PrimaryType

TypeOrVoid
	= Type / Void

Type_noArray
	= ClassOrInterfaceType / PrimaryType

ReferenceType
	= ArrayType
	/ ClassOrInterfaceType

ArrayType
	= Type_noArray {@ _ (@[2]Annotations)? _ "[" _ "]" @[1]{ `Array` #Name } #Type }+

SimpleClassOrInterfaceType
	= { (@[2]Annotations _ )? @[1]Name _ (@[0]TypeArguments)? #Type }

ClassOrInterfaceType
	= SimpleClassOrInterfaceType {@ _ "." _ @SimpleClassOrInterfaceType #Field }*

ClassOrInterfaceTypes
	= { @ClassOrInterfaceType ( _ "," _ @ClassOrInterfaceType)* #List }

TypeParameters
	= { "<" _ @TypeParameter ( _ "," _ @TypeParameter)* _ ">" #List }

TypeParameter
	= { (@[0]Annotations _ )? @[1]Name ( _ "extends" _ @[2]{ @ReferenceType ( _ "&" _ @ReferenceType)* #List } )? #TypeParameter }

TypeArguments
	= { "<" _ @TypeArgument ( _ "," _ @TypeArgument)* _ ">" #List }

TypeArgument
	= ReferenceType
	/ { (@[0]Annotations _ )? "?" _ ( "extends" _ @[1]ReferenceType / "super" _ @[2]ReferenceType)? #AnyType }

PrimaryType
	= { (@[2]Annotations _ )? @[1]PrimaryTypeName #Type }

Void
	= { (@[2]Annotations _ )? @[1]VoidName #Type }

//TypeName
TypeName
	= ReferenceTypeName / PrimaryTypeName

TypeOrVoidName
	= TypeName / VoidName

TypeName_noArray
	= ClassOrInterfaceTypeName / PrimaryTypeName

ReferenceTypeName
	= ArrayTypeName
	/ ClassOrInterfaceTypeName

ArrayTypeName
	= TypeName_noArray {@ _ "[" _ "]" @{ `Array` #Name } #QualifiedName }+

SimpleClassOrInterfaceTypeName
	= Name

ClassOrInterfaceTypeName
	= QualifiedName

PrimaryTypeName
	= { PRIMARY_TYPE #Name }

VoidName
	= { "void" #Name }

PRIMARY_TYPE
	= "byte"
	/ "char"
	/ "short"
	/ "int"
	/ "long"
	/ "float"
	/ "double"
	/ "boolean"


// Block, Statement
//===================

Block
	= { "{" _ ((@Statement / @LocalDeclaration) _ )* "}" #Block }

LocalDeclaration
	= VariableDeclaration _ ";"
	/ ClassDeclaration

Statement
	= Block
	/ { "assert" _ @Expression _ ( ":" _ @Expression)? _ ";" #Assert }
	/ { "if" _   "(" _ @Expression _ ")" _ @Statement ( _ "else" _ @Statement)? #If }
	/ { "switch" _ "(" _ @Expression _ ")" _ @Statement #Switch }
	/ { "while" _  "(" _ @Expression _ ")" _ @Statement #While }
	/ { "do" _ @Statement _ "while" _ "(" _ @Expression _ ")" _ ";" #DoWhile }
	/ { "for" _ "(" _ (@StatementExpressionList)? _ ";" _ (@Expression)? _ ";" _ (@StatementExpressionList)? _ ")" _ @Statement #For }
	/ { "for" _ "(" _  @VariableDeclaration       _ ";" _ (@Expression)? _ ";" _ (@StatementExpressionList)? _ ")" _ @Statement #For }
	/ { "for" _ "(" _ (@VariableModifiers)? _ @Type _ @Name _ ":" _ @Expression _ ")" _ @Statement #ForEach }
	/ { "goto" _ @Name _ ";" #Goto }
	/ { "continue" _ (@Name)? _ ";" #Continue }
	/ { "break" _ (@Name)? _ ";" #Break }
	/ { "return" _ (@Expression)? _ ";" #Return }
	/ { "try" _ @ResourceStatement _ @Block ( _ @CatchBlock)* ( _ @{ "finally" _ @Block #Finally })? #Try }
	/ { "try" _ @Block _ (@CatchBlock _ )* @{ "finally" _ @Block #Finally } #Try }
	/ { "try" _ @Block ( _ @CatchBlock)+ #Try }
	/ { "throw" _ @Expression _ ";" #Throw }
	/ { "synchronized" _ "(" _ @Expression _ ")" _ @Block #Synchronized }
	/ { "case" _ @ConstantExpression _ ":" _ (!"case" !"default" @Statement)* #SwitchCase }
	/ { "default" _ ":" _ (!"case" @Statement)* #SwitchDefault }
	/ { @Name _ ":" #Label }
	/ StatementExpression _ ";"
	/ { ";" #Empty }

//trycatch
ResourceStatement
	= "(" _ { @Resource ( _ ";" _ @Resource)* #ResourceList } _ ";"? _ ")"

Resource
	= { (@VariableModifiers _ )? @Type _ @Name _ "=" _ @Expression #Declaration }

CatchBlock
	= { "catch" _ "(" _ @CatchParameter _ ")" _ @Block #Catch }

CatchParameter
	= { (@VariableModifiers _ )? @ClassOrInterfaceType ( _ "|" _ @ClassOrInterfaceType )* _ @Name #Declaration }


//Expression
//==========

Expression
	= LambdaExpression
	/ AssignmentExpression
	/ ConditionalExpression

StatementExpression
	= AssignmentExpression
	/ PostfixExpression
	/ PrefixIncrementExpression
	/ PrefixDecrementExpression

StatementExpressionList
	= { @StatementExpression ( _ "," _ @StatementExpression)* #List }

ConstantExpression
	= ConditionalExpression

AssignmentExpression
	= { @PostfixExpression _ addAssignmentOperator _ @Expression }

addAssignmentOperator
	= "="    #Assign
	/ "*="   #AssignMul
	/ "/="   #AssignDiv
	/ "%="   #AssignMod
	/ "+="   #AssignAdd
	/ "-="   #AssignSub
	/ "<<="  #AssignLeftShift
	/ ">>="  #AssignRightShift
	/ ">>>=" #AssignLogicalRightShift
	/ "&="   #AssignBitwiseAnd
	/ "^="   #AssignBitwiseXor
	/ "|="   #AssignBitwiseOr

ConditionalExpression
	= LogicalORExpression {@ _ "?" _  @Expression _ ":" _ @LogicalORExpression #Conditional }*

LogicalORExpression
	= LogicalANDExpression {@ _ "||" _ @LogicalANDExpression #LogicalOr }*

LogicalANDExpression
	= InclusiveORExpression {@ _ "&&" _ @InclusiveORExpression #LogicalAnd }*

InclusiveORExpression
	= ExclusiveORExpression {@ _ "|" _ @ExclusiveORExpression #BitwiseOr }*

ExclusiveORExpression
	= ANDExpression {@ _ "^" _ @ANDExpression #BitwiseXor }*

ANDExpression
	= EqualityExpression {@ _ "&" _ @EqualityExpression #BitwiseAnd }*

EqualityExpression
	= RelationalExpression {@ _ ( "==" #Equals / "!=" #NotEquals ) _ @RelationalExpression }*

relationalOperator
	= "<=" #LessThanEquals
	/ ">=" #GreaterThanEquals
	/ "<"  #LessThan
	/ ">"  #GreaterThan

RelationalExpression
	= ShiftExpression {@ _ ( relationalOperator _ @ShiftExpression / "instanceof" _ @ReferenceType #InstanceOf ) }*

ShiftExpression
	= AdditiveExpression {@ _ ( "<<" #LeftShift / ">>" #RightShift / ">>>" #LogicalRightShift ) _ @AdditiveExpression }*

AdditiveExpression
	= MultiplicativeExpression {@ _ ( "+" #Add / "-" #Sub ) _ @MultiplicativeExpression }*

MultiplicativeExpression
	= CastExpression {@ _ ( "*" #Mul / "/" #Div / "%" #Mod ) _ @CastExpression }*

CastExpression
	= { "(" _ @Type _ ")" _ @CastExpression #Cast }
	/ UnaryExpression

UnaryExpression
	= PrefixIncrementExpression
	/ PrefixDecrementExpression
	/ { "+"  _ @CastExpression #Plus }
	/ { "-"  _ @CastExpression #Minus }
	/ { "~"  _ @CastExpression #Compl }
	/ { "!"  _ @CastExpression #Not }
	/ PostfixExpression

PrefixIncrementExpression
	= { "++" _ @CastExpression #PrefixInc }

PrefixDecrementExpression
	= { "--" _ @CastExpression #PrefixDec }

PostfixExpression
	= PrimaryExpression {@ _ (addMethodCall / addInstanceCreation / addIndex / addField / addMethodReference / addInc / addDec ) }*

addMethodCall
	= "." _ (@[3]TypeArguments)? _ @[1]Name _ "(" _ @[2]ArgumentList _ ")" #Method

addInstanceCreation
	= "." _ "new" _ (@[3]TypeArguments / "<" _ ">" )? _ @[1]SimpleClassOrInterfaceType _ "(" _ @[2]ArgumentList _ ")" ( _ @[4]ClassBody)? #New

addIndex
	= "[" _ @[1]Expression _ "]" #Index

addField
	= "." _ @[1]Name _ !'(' #Field

addMethodReference
	= "::" _ (@[3]TypeArguments)? _ @[1]Name #MethodRef

addInc
	= "++" #SuffixInc

addDec
	= "--" #SuffixDec

PrimaryExpression
	= Constant
	/ "(" _ Expression _ ")"
	/ { @TypeOrVoidName _ "." _ "class" #TypeOf }
	/ This
	/ { @ClassOrInterfaceTypeName _ "." _ @This  #Field }
	/ Super
	/ { @ClassOrInterfaceTypeName _ "." _ @Super #Field }
	/ MethodInvocationExpression
	/ InstanceCreationExpression
	/ ArrayCreationExpression
	/ MethodReference
	/ Name

This
	= { "this" #This }

Super
	= { "super" #Super }

MethodInvocationExpression
	= { @[1]Name _ "(" _ @[2]ArgumentList _ ")" #Method }
	/ { @[0]ClassOrInterfaceTypeName _ "." _ (@[3]TypeArguments)? _ @[1]Name _ "(" _ @[2]ArgumentList _ ")" #Method }

ArgumentList
	= { (@Expression ( _ "," _ @Expression )* )? #List }

InstanceCreationExpression
	= { "new" _ (@[3]TypeArguments / "<" _ ">" )? _ @[1]ClassOrInterfaceType _ "(" _ @[2]ArgumentList _ ")" ( _ @[4]ClassBody)? #New }

//FIXME with size
ArrayCreationExpression
	= { "new" _ @(Type_noArray {@ _ (@Annotations)? _ "[" _ @Expression _ "]" #ArraySize }+ {@ _ (@Annotations)? _ "[" _ "]" #ArraySize }* ) #ArrayCreation }
	/ { "new" _ @[1]ArrayType _ @[4]ArrayInitializer #New }

MethodReference
	= { @[0]ReferenceType _ "::" _ (@[3]TypeArguments)? _ @[1]( Name / { "new" #Name } ) #MethodRef }

LambdaExpression
	= { @LambdaParameters _ "->" _ @LambdaBody #Lambda }

LambdaParameters
	= "(" _ MethodParamList _ ")"
	/ "(" _ InferredFormalParameterList _ ")"
	/ { @Name #List }

InferredFormalParameterList
	= { @Name ( _ "," _ @Name)* #List }

LambdaBody
	= Expression
	/ Block


//Literal
//=======

Constant
	= JavaFloat
	/ JavaInteger
	/ JavaBoolean
	/ JavaString
	/ JavaChar
	/ JavaNull

JavaFloat
	= { (FLOAT / DECIMAL) #Float } [fF]
	/ { FLOAT #Double } [dD]?
	/ { DECIMAL #Double } [dD]

FLOAT
	= FRACTION EXPONENT?
	/ DIGIT+ EXPONENT

FRACTION
	= !'_' ('_'* DIGIT)* '.' DIGIT ('_'* DIGIT)*
	/ DIGIT ('_'* DIGIT)* '.'


EXPONENT
	= [eE] [+\-]? DIGIT ('_'* DIGIT)*

JavaInteger
	= { ( DECIMAL #Long    / HEXADECIMAL #HexLong    / BINARY #BinaryLong    / OCTAL #OctalLong    ) } [lL]
	/ { ( DECIMAL #Integer / HEXADECIMAL #HexInteger / BINARY #BinaryInteger / OCTAL #OctalInteger ) }

DECIMAL
	= '0' ![bBxX0-9_]
	/ [1-9] ('_'* DIGIT)*

HEXADECIMAL
	= '0' [xX] HEX ('_'* HEX)*

BINARY
	= '0' [bB] [01] ('_'* [01])*

OCTAL
	= '0' ('_'* [0-7])*

JavaBoolean
	= { "true"  #True }
	/ { "false" #False }

JavaString
	= '"' { STRING_CONTENT* #String } '"'

JavaChar
	= '\'' { CHAR_CONTENT #Character } '\''

STRING_CONTENT
	= ESCAPE
	/ !["\n\\] .

CHAR_CONTENT
	= ESCAPE
	/ ![\'\n\\] .

ESCAPE
	= SIMPLE_ESCAPE
	/ OCTAL_ESCAPE
	/ HEX_ESCAPE

SIMPLE_ESCAPE
	= '\\' ['\"\\bfnrt]

OCTAL_ESCAPE
	= '\\' [0-3] [0-7] [0-7]
	/ '\\' [0-7] [0-7]
	/ '\\' [0-7]

HEX_ESCAPE
	= '\\' 'u'+ HEX4

JavaNull
	= { "null" #Null }
